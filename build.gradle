plugins {
	id 'java'
	id 'eclipse'
	id 'org.springframework.boot' version '2.5.14'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'jacoco'
}

group = 'cl.jose.huenul.springboot.globallogic'
version = '1.0.0'

java {
	sourceCompatibility = '11'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

def springBootLibraries = [

	'org.springframework.boot:spring-boot-starter-data-jpa',
	'org.springframework.boot:spring-boot-starter-security',
	'org.springframework.boot:spring-boot-starter-web',
	'org.springframework.boot:spring-boot-starter-validation'

]

def testLibraries = [
	'org.springframework.boot:spring-boot-starter-test',
	'org.springframework.security:spring-security-test',
	'org.junit.jupiter:junit-jupiter-engine:5.6.0',
	'org.junit.jupiter:junit-jupiter-api:5.6.0'
]

def externalLibraries = [
	'io.jsonwebtoken:jjwt-api:0.11.2',
	'io.jsonwebtoken:jjwt-impl:0.11.2',
	'io.jsonwebtoken:jjwt-jackson:0.11.2'
]

jacoco {
    toolVersion = "0.8.12"
}


dependencies {

	implementation springBootLibraries
	implementation externalLibraries
	testImplementation testLibraries
	
	compileOnly 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
	useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}


jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoReport')
    }
    
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['cl/jose/huenul/**/pojo',
                    		  'cl/jose/huenul/**/config',
                    		  'cl/jose/huenul/**/entity',
                    		  'cl/jose/huenul/**/exception',
                    		  'cl/jose/huenul/**/constants',
                    		  'cl/jose/huenul/**/service/*Service.class',
                    		  '**/Application.class'
                              ])
        })
    }
}
